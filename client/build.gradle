buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.moowork.gradle:gradle-node-plugin:0.13"
  }
}
apply plugin: "com.moowork.node"
apply plugin: "java"
apply plugin: 'idea'

idea {
  module {
    excludeDirs += file('./dist')
  }
}

jar {
  baseName 'client'
  from 'dist/'
  eachFile { details ->
    details.path = details.path.startsWith('META-INF') ?: 'static/'+details.path
  }
  // Jar contains duplicate empty folders, see Gradle issue:
  // http://issues.gradle.org/browse/GRADLE-1830
  // so we need to set includeEmptyDirs to false
  includeEmptyDirs = false

}

task npmBuildProd(dependsOn: npmInstall, type: NpmTask) {
  inputs.dir "src"
  inputs.file "packages.json"
  outputs.dir "dist"
  args = ['run', 'build.prod']
}

task testJS(type: NpmTask) {
  inputs.dir "src"
  inputs.file "packages.json"
  outputs.dir "dist"
  args = ['run', 'test']
}

test.dependsOn testJS

jar.dependsOn npmBuildProd

