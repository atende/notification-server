buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'flyway'
apply plugin: 'download-task'

version = "1.0-SNAPSHOT"

sourceCompatibility = 1.8

ext {
  appName = 'Notification Server'
  wildflyVersion = '10.0.0.Final' // Used for download and setup widlfly
  libraryVersions = [
        junit: '4.11', arquillian: '1.1.5.Final',
        slf4j: '1.7.5', log4j: '1.7.5',
        keycloak: '1.9.1.Final', deltaSpike: '1.5.2'

  ]
}
// If profile is set try to apply the profile file, if not, use the default profile
if(!hasProperty('profile')){
    apply from: 'wildfly-managed-profile.gradle'
}else{
    apply from: profile +'-profile.gradle'
}

// compileOnly scope configuration
// Javadoc generation customization
javadoc {
    project.configure(options){
        charSet = "UTF-8"
        windowTitle = "$appName"
        header = "<b>$appName</b>"
        bottom = "Created by <a href='http://www.atende.info'> Atende Tecnology </a>"
    }
}

repositories {
    mavenCentral()
    maven {
      url "http://repository.jboss.org/nexus/content/groups/public"
    }
    maven{
        url 'https://oss.sonatype.org/content/repositories/snapshot'
    }
}


dependencies {
    compileOnly 'org.jboss.spec:jboss-javaee-all-7.0:1.0.3.Final'
    testCompile group: 'junit', name: 'junit', version: libraryVersions.junit
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: libraryVersions.arquillian
    testCompile 'com.icegreen:greenmail:1.3.1b'
    compileOnly 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
    compileOnly 'org.hibernate:hibernate-validator:5.2.4.Final'
    compileOnly "org.keycloak:keycloak-core:$libraryVersions.keycloak"
    compileOnly "org.keycloak:keycloak-adapter-core:$libraryVersions.keycloak"
    compile 'commons-beanutils:commons-beanutils:1.9.2'
    compile "org.apache.deltaspike.core:deltaspike-core-api:$libraryVersions.deltaSpike"
    runtime "org.apache.deltaspike.core:deltaspike-core-impl:$libraryVersions.deltaSpike"
    compile "org.flywaydb:flyway-core:3.2.1"
    compile group: 'info.atende.webutil', name: 'wu-jpa', version: '0.1.0'
//    compile 'org.jadira.usertype:usertype.extended:3.2.0.GA'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task downloadWildfly << {
  download {
    src "http://download.jboss.org/wildfly/${wildflyVersion}/wildfly-${wildflyVersion}.zip"
    overwrite false
    dest buildDir
  }

}
task unzipWildfly(dependsOn: downloadWildfly, type: Copy){
  from zipTree("$buildDir/wildfly-${wildflyVersion}.zip")
  into buildDir
}
