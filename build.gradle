buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}
apply plugin: 'download-task'
apply plugin: 'java'
apply plugin: 'war'
version = "1.0-SNAPSHOT"

sourceCompatibility = 1.7

ext {
  libraryVersions = [
        junit: '4.11', arquillian: '1.1.5.Final',
        slf4j: '1.7.5', log4j: '1.7.5'

  ] 
}
// If profile is set try to apply the profile file, if not, use the default profile
if(!hasProperty('profile')){
    apply from: 'weld-ee-embedded-profile.gradle'
}else{
    apply from: profile +'-profile.gradle'
}

// Provided scope configuration
configurations {
      provided {
          dependencies.all { dep ->
              configurations.default.exclude group: dep.group, module: dep.name
          }
      }
      compile.extendsFrom provided
  }
// Javadoc generation customization
javadoc {
    appName = 'Java Enterprise Skeleton'
    project.configure(options){
        charSet = "UTF-8"
        windowTitle = "$appName"
        header = "<b>$appName</b>"
        bottom = "Created by <a href='http://www.atende.info'> Atende Tecnology </a>"
    }
}

repositories {
    mavenCentral()
    maven { 
      url "http://repository.jboss.org/nexus/content/groups/public" 
    }
}


dependencies {
    provided 'org.jboss.spec:jboss-javaee-all-7.0:1.0.1.Final'
    testCompile group: 'junit', name: 'junit', version: libraryVersions.junit
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: libraryVersions.arquillian
    compile 'org.jadira.usertype:usertype.extended:3.2.0.GA'


}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task downloadWildfly << {
  download {
    src 'http://download.jboss.org/wildfly/8.1.0.Final/wildfly-8.1.0.Final.zip'
    overwrite false
    dest buildDir
  }
  
}
task unzipWildfly(dependsOn: downloadWildfly, type: Copy){
  from zipTree("$buildDir/wildfly-8.1.0.Final.zip")
  into buildDir 
}
