apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'

version = "1.0-SNAPSHOT"

sourceCompatibility = 1.7

ext {
  libraryVersions = [
        junit: '4.+', arquillian: '1.1.5.Final',
        javaeeApi: '7.0',  slf4j: '1.7.5', log4j: '1.7.5'

  ] 
}
// If profile is set try to apply the profile file, if not, use the wildfly profile
if(!hasProperty('profile')){
    apply from: 'wildfly-managed-profile.gradle'
}else{
    apply from: profile +'-profile.gradle'
}



configurations {
   provided
}


repositories {
    mavenCentral()
    maven { 
      url "http://repository.jboss.org/nexus/content/groups/public" 
    }
}


dependencies {
    provided 'javax:javaee-api:7.0'
    testCompile group: 'junit', name: 'junit', version: libraryVersions.junit
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: libraryVersions.arquillian

}

// Configure source class path
sourceSets {
  main {
         compileClasspath += [configurations.provided]
  }
  test {
       compileClasspath += [configurations.provided]
  }
}

// optional: if using 'idea' plugin
// Configure intellij IDEA classpath for provided scope
idea {
    module{
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
