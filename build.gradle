buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'flyway'
apply plugin: 'download-task'

version = "1.0-SNAPSHOT"

sourceCompatibility = 1.8

ext {
  appName = 'Notification Server'
  wildflyVersion = '8.1.0.Final' // Used for download and setup widlfly
  libraryVersions = [
        junit: '4.11', arquillian: '1.1.5.Final',
        slf4j: '1.7.5', log4j: '1.7.5',
        keycloak: '1.0-rc-1', deltaSpike: '1.0.1'

  ] 
}
// If profile is set try to apply the profile file, if not, use the default profile
if(!hasProperty('profile')){
    apply from: 'wildfly-managed-profile.gradle'
}else{
    apply from: profile +'-profile.gradle'
}

// Provided scope configuration
configurations {
      provided {
          dependencies.all { dep ->
              configurations.default.exclude group: dep.group, module: dep.name
          }
      }
      compile.extendsFrom provided
  }
// Javadoc generation customization
javadoc {    
    project.configure(options){
        charSet = "UTF-8"
        windowTitle = "$appName"
        header = "<b>$appName</b>"
        bottom = "Created by <a href='http://www.atende.info'> Atende Tecnology </a>"
    }
}

repositories {
    mavenCentral()
    maven { 
      url "http://repository.jboss.org/nexus/content/groups/public" 
    }
    maven{
        url 'https://oss.sonatype.org/content/repositories/snapshot'
    }
}


dependencies {
    provided 'org.jboss.spec:jboss-javaee-all-7.0:1.0.1.Final'
    testCompile group: 'junit', name: 'junit', version: libraryVersions.junit
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: libraryVersions.arquillian
    testCompile 'com.icegreen:greenmail:1.3.1b'
    provided 'org.hibernate:hibernate-entitymanager:4.3.5.Final'
    provided 'org.hibernate:hibernate-validator:5.1.1.Final'
    provided "org.keycloak:keycloak-core:$libraryVersions.keycloak"
    provided "org.keycloak:keycloak-adapter-core:$libraryVersions.keycloak"
    compile 'commons-beanutils:commons-beanutils:1.9.2'
    compile "org.apache.deltaspike.core:deltaspike-core-api:$libraryVersions.deltaSpike"
    runtime "org.apache.deltaspike.core:deltaspike-core-impl:$libraryVersions.deltaSpike"
    compile "org.flywaydb:flyway-core:3.0"
    compile group: 'info.atende.webutil', name: 'wu-jpa', version: '0.1.0-SNAPSHOT', changing: true
//    compile 'org.jadira.usertype:usertype.extended:3.2.0.GA'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task downloadWildfly << {
  download {
    src "http://download.jboss.org/wildfly/${wildflyVersion}/wildfly-${wildflyVersion}.zip"
    overwrite false
    dest buildDir
  }
  
}
task unzipWildfly(dependsOn: downloadWildfly, type: Copy){
  from zipTree("$buildDir/wildfly-${wildflyVersion}.zip")
  into buildDir 
}
